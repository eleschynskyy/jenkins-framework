pipeline {
    agent any

    parameters {
        string(name: 'HOST', defaultValue: 'http://localhost', description: 'Base URL for all tools')
        string(name: 'USERS', defaultValue: '10', description: 'Virtual users for Gatling and JMeter')
        string(name: 'RAMP', defaultValue: '30', description: 'Ramp-up duration (seconds)')
        string(name: 'DURATION', defaultValue: '60', description: 'Steady-state duration (seconds)')
        booleanParam(name: 'RUN_GATLING', defaultValue: true, description: 'Execute Gatling stage')
        booleanParam(name: 'RUN_JMETER', defaultValue: true, description: 'Execute JMeter stage')
        booleanParam(name: 'RUN_LIGHTHOUSE', defaultValue: true, description: 'Execute Lighthouse stage')
    }

    stages {
        stage('Prepare Reports') {
            steps {
                script {
                    env.REPORT_ROOT = "reports/build-${env.BUILD_NUMBER}"
                }
                sh """
                    #!/usr/bin/env bash
                    set -euo pipefail
                    rm -rf "${env.REPORT_ROOT}"
                    mkdir -p "${env.REPORT_ROOT}/gatling" "${env.REPORT_ROOT}/jmeter" "${env.REPORT_ROOT}/lighthouse"
                """
            }
        }

        stage('Validate Selection') {
            steps {
                script {
                    if (!params.RUN_GATLING && !params.RUN_JMETER && !params.RUN_LIGHTHOUSE) {
                        error 'Select at least one tool to execute.'
                    }
                }
            }
        }

        stage('Gatling') {
            when {
                expression { return params.RUN_GATLING }
            }
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    dir('tests/gatling') {
                        sh """
                            #!/usr/bin/env bash
                            set -euo pipefail
                            chmod +x mvnw
                            ./mvnw -B gatling:test \\
                                -Dgatling.simulationClass=simulations.testSimulation \\
                                -DbaseURL='${params.HOST}' \\
                                -Dusers='${params.USERS}' \\
                                -Dramp='${params.RAMP}' \\
                                -Dduration='${params.DURATION}'
                            dest='../../${env.REPORT_ROOT}/gatling'
                                rm -rf "\${dest}"
                                mkdir -p "\${dest}"
                                mkdir -p target/gatling
                                cp -R target/gatling/. "\${dest}/"
                        """
                    }
                }
            }
        }

        stage('JMeter') {
            when {
                expression { return params.RUN_JMETER }
            }
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh """
                        #!/usr/bin/env bash
                        set -euo pipefail
                        ts_dir='${env.REPORT_ROOT}/jmeter'
                        rm -rf "\${ts_dir}"
                        mkdir -p "\${ts_dir}/report"
                        jmeter -n \\
                            -t tests/jmeter/checkout_test_plan.jmx \\
                            -l "\${ts_dir}/results.jtl" \\
                            -e -o "\${ts_dir}/report" \\
                            -JbaseURL='${params.HOST}' \\
                            -Jusers='${params.USERS}' \\
                            -Jramp='${params.RAMP}' \\
                            -Jduration='${params.DURATION}'
                    """
                }
            }
        }

        stage('Lighthouse') {
            when {
                expression { return params.RUN_LIGHTHOUSE }
            }
            steps {
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    dir('tests/lighthouse') {
                        sh """
                            #!/usr/bin/env bash
                            set -euo pipefail
                            npm ci
                            export LH_BASE_URL='${params.HOST}'
                            node shopizer.js
                            dest='../../${env.REPORT_ROOT}/lighthouse'
                                rm -rf "\${dest}"
                                mkdir -p "\${dest}"
                                mv flow.report.html "\${dest}/flow-${env.BUILD_NUMBER}.html"
                        """
                    }
                }
            }
        }
    }

    post {
        always {
            archiveArtifacts artifacts: 'reports/**/*', allowEmptyArchive: true
        }
        cleanup {
            cleanWs()
        }
    }
}
